WHITESPACE = _{ " " }

char = { ASCII_ALPHANUMERIC | " " | "+" }
name = @{ char+ }
value = { (!NEWLINE ~ ANY)* }
section = { "[" ~ name ~ "]" }
property = { name ~ ":" ~ value }
comment = _{ ";" ~ (!NEWLINE ~ ANY)* }
contents = _{ (comment | property)? }

item = { ASCII_ALPHA+ }
effect = { ASCII_ALPHA* }
list = { item ~ ("," ~ item)* }
format = { "Format:" ~ list }

number = { DECIMAL_NUMBER+ }
hour = { DECIMAL_NUMBER }
min = { DECIMAL_NUMBER{2} }
sec = { DECIMAL_NUMBER{2} }
hun = { DECIMAL_NUMBER{2} }
time = ${ hour ~ ":" ~ min ~ ":" ~ sec ~ "." ~ hun }

dialogue_name = { (ASCII_ALPHANUMERIC | "_")* }
dialogue = { "Dialogue:" ~ number ~ "," ~ time ~ "," ~ time ~ "," ~ dialogue_name ~ "," ~ dialogue_name ~ "," ~ number ~ "," ~ number ~ "," ~ number ~ "," ~ effect ~ "," ~ value }

events = _{ "[Events]" ~ NEWLINE ~ ((format | dialogue | contents) ~ NEWLINE)+ }
other = _{ (section) ~ NEWLINE ~ (contents ~ NEWLINE)+ }

bom = _{ "\u{feff}" }
file = {
    SOI ~ bom? ~
    (events | other)* ~
    EOI
}